! Macro zum Darstellen der Elementdicke
! zeigt jeweils die Elemente mit derselben Dicke
!
! darzustellende Elemente und Knoten sind bereits ausgewaehlt
!
! wie di71, aber uebernimmt stits
! wie di73, aber sortiert die Blechstärken nach sectype
! Komponentenname tbl_i
! 21.01.10 kurz

! Elemente werden durchsichtig dargestellt	10.01.14 kurz
! /type muss auf /type,,6 gestellt sein



! arg1 : = 0 : Bleche
!          1 : Schweissnaht
!
! englisch_ :            Variable (aussen gesetzt) = 1 : englische Texte
!                                                  = 2 : französische Texte
!

!/nopr
/plo,info,on
tsize_=0.8
/tspec,15,tsize_,1,0,0
/anno,tmod,1
/anum,1

cm,di7_,elem
cm,di8_,node



! deutsch / englisch

*if,englisch_,lt,0.001,then
englisch_=0
*endif

*if,englisch_,eq,0,then
bl_='Bleche t = '
*if,arg1,eq,1,then
bl_='Naehte a = '
*endif
*elseif,englisch_,eq,1,then
bl_='Plates t = '
*if,arg1,eq,1,then
bl_='Welds a = '
*endif
*elseif,englisch_,eq,2,then
bl_='Plaques t = '
*if,arg1,eq,1,then
bl_='Soudures a = '
*endif
*endif


! max. Sectype ermitteln

!ranz_=0

*get,eanz_,elem,,count

*do,i,1,9999
esel,u,sec,,i
*get,eanz_2,elem,,count
  *if,eanz_2,lt,eanz_,then
  smin_=i
  eanz_=-1
  *endif
  *if,eanz_2,eq,0,then
  smax_=i
  *exit
  *endif

*enddo

cmsel,s,di7_


! Blechdicken ermitteln

*set,vec1
*dim,vec1,,smax_-smin_+1
*set,vec2
*dim,vec2,,smax_-smin_+1

j=1
*do,i,smin_,smax_
n1=0

esel,s,sec,,i
esel,r,ename,,181

*get,eanz_3,elem,,count
*if,eanz_3,le,0,cycle

*get,ielem_,elem,,num,min
*get, etyp_, elem, ielem_, attr, type
*get, enam_,etyp,etyp_, attr,enam

*if,enam_,ne,181,then
vec2(j)=0
*else
*get,n1,shel,i,prop,tthk
vec1(j)=i
vec2(j)=n1
j=j+1
*endif
*enddo


! der Dicke nach sortieren

*do,i,1,smax_-smin_+1
*do,j,i+1,smax_-smin_+1
*if,vec2(i),gt,vec2(j),then
ii=vec1(i)
vec1(i)=vec1(j)
vec1(j)=ii
ii=vec2(i)
vec2(i)=vec2(j)
vec2(j)=ii
*endif
*enddo
*enddo




! darstellen

/anno,dele
cmsel,s,di8_
ilast=0.2
jj=1

*do,i,1,smax_-smin_+1
	cmsel,s,di7_
	/trlcy,elem,1
	!/color,elem,lgra
	/color,elem,blac
	*if,vec2(i),gt,ilast,then
		esel,none
		  *do,j,1,smax_-smin_+1
			    *if,vec2(j),eq,vec2(i),then
					esel,a,sec,,vec1(j)
			    *endif
		  *enddo
		cmse,r,di7_
		/trlcy,elem,0
		*if,jj,le,14,then
			/color,elem,%jj%
		*else
			/color,elem,%jj-14%
		*endif

		*get,n2,elem,,count
		*if,n2,gt,0,then
			n1=vec2(i)*10
			cmse,s,di8_
			/tlab, +1.040,-0.98,%bl_% %n1% mm

			cm,tbl_%jj%,elem
			jj=jj+1
			cmsel,a,di7_
			eplo
			!/era
			!/aut
			/anno,dele
		*endif
		ilast=vec2(i)
	*endif
	

*enddo


cmse,s,di7_
cmse,s,di8_
cmde,di7_
cmde,di8_
*set,ranz_
*set,bl_
/trlcy,elem,
/color,defa
!*set,vec1
!*set,vec2
